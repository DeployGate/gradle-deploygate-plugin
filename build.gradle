apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group = 'com.deploygate'
archivesBaseName = 'gradle'
version = new File(rootProject.projectDir, 'src/main/resources/VERSION').text.trim()

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

repositories {
    google()
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()

//    compile 'com.android.tools.build:gradle:3.2.0-beta05'
    compile 'com.android.tools.build:bundletool:0.5.0'
    compile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
    compile 'org.apache.httpcomponents:httpmime:4.2.5'
    runtime 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
    runtime 'org.apache.httpcomponents:httpmime:4.2.5'

    testCompile 'junit:junit:4.12'
}

task javadocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'javadoc'
    from "${buildDir}/javadoc"
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

// Release
bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'maven'
        name = project.archivesBaseName
        userOrg = 'deploygate'
        licenses = ['Apache-2.0']
        websiteUrl = 'https://github.com/DeployGate/gradle-deploygate-plugin'
        issueTrackerUrl = 'https://github.com/DeployGate/gradle-deploygate-plugin/issues'
        vcsUrl = 'https://github.com/DeployGate/gradle-deploygate-plugin.git'
        githubRepo = 'DeployGate/gradle-deploygate-plugin'
        version {
            name = project.version
            released = new Date()
        }
    }

    configurations = ['archives']
}

// For publishToMavenLocal
publishing {
    publications {
        pluginPublication(MavenPublication) {
            from components.java
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
        }
    }
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}